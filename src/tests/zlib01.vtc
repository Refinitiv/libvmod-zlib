varnishtest "ZLIB: request uncompressing. Test Content-Encoding: gzip"

server s1 {
        rxreq
        expect req.http.content-encoding == <undef>
        expect req.http.content-length == 210
        txresp -status 200

        rxreq
        expect req.http.content-encoding == <undef>
        expect req.http.content-length == 211
        txresp -status 200
} -start

varnish v1 -vcl+backend {
    import ${vmod_zlib};

    sub vcl_recv {
            if (zlib.unzip_request() == -1) {
                    return (synth(400, "can't uncompress request's body"));
            }
    }
} -start

client c1 {
        txreq -req POST -gzipbody {<?xml version="1.0" encoding="utf-8"?><Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
<Header>
<userIdentity/>
</Header>
<Body>
<Test xmlns="http://schemas.reuters.com/mytest"/>
</Body>
</Envelope>}
        rxresp
        expect resp.status == 200
        txreq -req POST -body {<?xml version="1.0" encoding="utf-8"?><Envelope xmlns="http://schemas.xmlsoap.org/soap/envelope/">
<Header>
<userIdentity/>
</Header>
<Body>
<Test xmlns="http://schemas.reuters.com/mytest2"/>
</Body>
</Envelope>}
        rxresp
        expect resp.status == 200
} -run
